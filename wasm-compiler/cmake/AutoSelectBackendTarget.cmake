function(AutoSelectBackendTarget)	
	if(${CMAKE_GENERATOR} MATCHES "Visual Studio")
		message("Visual Studio Setting")
		if(${CMAKE_GENERATOR_PLATFORM} MATCHES "ARM64")
			set(NOW_TARGET "aarch64")
		elseif(${CMAKE_GENERATOR_PLATFORM} MATCHES "ARM")
			set(NOW_TARGET "aarch32")
		elseif(${CMAKE_GENERATOR_PLATFORM} MATCHES "x64")
			set(NOW_TARGET "x86_64")
		else()
			set(NOW_TARGET "x86")
		endif()
	elseif((CMAKE_CXX_COMPILER_ID STREQUAL "GNU") OR CMAKE_CXX_COMPILER_ID STREQUAL "Clang" OR CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang")
		execute_process(COMMAND ${CMAKE_CXX_COMPILER} -v ERROR_VARIABLE output)
		if("${output}" MATCHES "Target:[ \t\r\n]+x86_64")
			set(NOW_TARGET "x86_64")
		elseif("${output}" MATCHES "Target:[ \t\r\n]+(aarch64|arm64)")
			set(NOW_TARGET "aarch64")
		elseif("${output}" MATCHES "Target:[ \t\r\n]+tricore")
			set(NOW_TARGET "tricore")
		endif()
	elseif(CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
		execute_process(COMMAND ${CMAKE_CXX_COMPILER} ERROR_VARIABLE output)
		if("${output}" MATCHES "x64")
			set(NOW_TARGET "x86_64")
		elseif("${output}" MATCHES "x86")
			set(NOW_TARGET "x86")
		elseif("${output}" MATCHES "ARM64")
			set(NOW_TARGET "aarch64")
		endif()
	elseif(CMAKE_CXX_COMPILER_ID STREQUAL "QCC")
		if("${CMAKE_CXX_FLAGS}" MATCHES "march=armv8")
			set(NOW_TARGET "aarch64")
		else()
			set(NOW_TARGET "x86_64")
		endif()
	elseif((CMAKE_CXX_COMPILER_ID STREQUAL "Tasking") AND (CMAKE_SYSTEM_PROCESSOR STREQUAL "tricore"))
		set(NOW_TARGET "tricore")
	endif()

	if(DEFINED NOW_TARGET)
		set(CXX_TARGET ${NOW_TARGET} PARENT_SCOPE)
		if (NOT "${BACKEND_INTERNAL}" STREQUAL "")
			if(NOT BACKEND_INTERNAL STREQUAL NOW_TARGET)
				message("Detected compiler target: ${NOW_TARGET}")
				message(WARNING "Specified backend does not match the compiler target")
			endif()
		else()
			set(BACKEND_INTERNAL ${NOW_TARGET} PARENT_SCOPE)
			message("Detected compiler target: ${NOW_TARGET}")
			message("No backend specified, auto-setting BACKEND to ${NOW_TARGET}")
		endif()
	endif()
endfunction(AutoSelectBackendTarget)